// Adapted from socket head cap screw exmaple.

// set units
@settings(defaultLengthUnit = mm, defaultAngleUnit = deg)

export boltDiameter = 11.5 // M12x1.75
export boltLength = 100
export boltHeadLength = boltDiameter
export boltHeadDiameter = 18
export boltHexDrive = 10.2
export boltHexFlatLength = boltHexDrive / (2 * cos(toRadians(30)))

export fn bolt () {
  
  // Create the head of the cap screw
  boltHead = startSketchOn(XZ)
    |> circle(
         center = [0, 0],
         radius = boltHeadDiameter / 2,
         tag = $topEdge
       )
    |> extrude(length = -boltHeadLength)
    |> fillet(radius = 1, tags = [topEdge, getOppositeEdge(topEdge)])
  
  // Define the sketch of the hex pattern on the screw head
  hexPatternSketch = startSketchOn(boltHead, 'start')
    |> startProfileAt([
         boltHexDrive / 2,
         boltHexFlatLength / 2
       ], %)
    |> angledLine({
         angle = 270,
         length = boltHexFlatLength
       }, %)
    |> angledLine({
         angle = 210,
         length = boltHexFlatLength
       }, %)
    |> angledLine({
         angle = 150,
         length = boltHexFlatLength
       }, %)
    |> angledLine({
         angle = 90,
         length = boltHexFlatLength
       }, %)
    |> angledLine({
         angle = 30,
         length = boltHexFlatLength
       }, %)
    |> close()
    |> extrude(length = -boltHeadLength * 0.75)
  
  boltBody = startSketchOn(boltHead, 'end')
    |> circle(center = [0, 0], radius = boltDiameter / 2, tag = $filletEdge)
    |> extrude(length = boltLength)
    |> chamfer(length = 2, tags = [getOppositeEdge(filletEdge)])


  return boltBody
}

bolt()
